/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    alias(libs.plugins.kotlinSerialization)
}

androidXMultiplatform {
    androidTarget()
    desktop()
    mingwX64()
    linux()
    mac()
    ios()
    tvos()
    watchos()
    js()
    wasmJs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                api(project(":savedstate:savedstate"))
                api(project(":compose:runtime:runtime"))
            }
        }

        commonTest {
            dependencies {
                implementation(project(":kruth:kruth"))
                implementation(libs.kotlinTest)
                implementation(libs.kotlinTestCommon)
                implementation(libs.kotlinTestAnnotationsCommon)
                implementation(libs.kotlinCoroutinesTest)
            }
        }

         androidMain {
            dependsOn(commonMain)
            dependencies {
                api("androidx.annotation:annotation:1.9.1")
                implementation("androidx.core:core-ktx:1.13.1")
            }
        }

        androidUnitTest {
            dependsOn(commonTest)
            dependencies {
                implementation(libs.robolectric)
            }
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation(libs.testExtJunit)
                implementation(libs.testCore)
                implementation(libs.testRunner)
                implementation(libs.testRules)
            }
        }

        nonAndroidMain {
            dependsOn(commonMain)
        }

        nonAndroidTest {
            dependsOn(commonTest)
        }

        targets.configureEach { target ->
            if (target.platformType !in [KotlinPlatformType.androidJvm, KotlinPlatformType.common]) {
                target.compilations["main"].defaultSourceSet.dependsOn(nonAndroidMain)
                target.compilations["test"].defaultSourceSet.dependsOn(nonAndroidTest)
            }
        }
    }
}

android {
    buildTypes.configureEach {
        consumerProguardFiles("proguard-rules.pro")
    }
    namespace = "androidx.savedstate.compose"
    experimentalProperties["android.lint.useK2Uast"] = false // TODO(b/345531033)
}

androidx {
    name = "Saved State Compose"
    type = SoftwareType.PUBLISHED_LIBRARY
    samples(project(":savedstate:savedstate-compose"))
    inceptionYear = "2024"
    description = "Compose integration with Saved State"
    metalavaK2UastEnabled = false
}
