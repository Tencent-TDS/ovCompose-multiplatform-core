/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    androidLibrary {
        namespace = "androidx.compose.material.ripple"
        withAndroidTestOnDeviceBuilder {
            it.compilationName = "instrumentedTest"
            it.defaultSourceSetName = "androidInstrumentedTest"
            it.sourceSetTreeName = "test"
        }
        withAndroidTestOnJvmBuilder {
            it.defaultSourceSetName = "androidUnitTest"
        }

        compileSdk = 35
        aarMetadata.minCompileSdk = 35
    }
    desktop()
    mac()
    ios()
    js()
    wasmJs()

    // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
    linuxX64Stubs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlib)
                api(project(":compose:foundation:foundation"))
                api(project(":compose:runtime:runtime"))

                implementation(project(":collection:collection"))
                implementation(project(":compose:animation:animation"))
                implementation(project(":compose:ui:ui-util"))
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlinTest)
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        jvmTest {
            dependsOn(commonTest)
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
            }
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":compose:test-utils"))
                implementation(project(":compose:foundation:foundation"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        nonAndroidMain {
            dependsOn(commonMain)
        }

        nonAndroidTest {
            dependsOn(commonTest)
        }

        desktopMain {
            dependsOn(jvmMain)
            dependsOn(nonAndroidMain)
        }

        desktopTest {
            dependsOn(jvmTest)
            dependsOn(nonAndroidTest)
        }

        nonJvmMain {
            dependsOn(commonMain)
            dependsOn(nonAndroidMain)
        }

        nonJvmTest {
            dependsOn(commonTest)
            dependsOn(nonAndroidTest)
        }

        targets.configureEach { target ->

            // TODO: Ignore stubs. Remove together with stubs target
            if (target.name.endsWith("Stubs")) return

            if (target.platformType == KotlinPlatformType.native) {
                target.compilations["main"].defaultSourceSet.dependsOn(nonJvmMain)
                target.compilations["test"].defaultSourceSet.dependsOn(nonJvmTest)
            } else if (target.platformType in [KotlinPlatformType.js, KotlinPlatformType.wasm]) {
                target.compilations["main"].defaultSourceSet.dependsOn(nonJvmMain)
                target.compilations["test"].defaultSourceSet.dependsOn(nonJvmTest)
            }
        }

        // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
        commonStubsMain {
            dependsOn(commonMain)
        }

        linuxx64StubsMain {
            dependsOn(commonStubsMain)
        }
    }
}

androidx {
    name = "Compose Material Ripple"
    type = SoftwareType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    mavenVersion = LibraryVersions.COMPOSE
    inceptionYear = "2020"
    description = "Material ripple used to build interactive components"
    legacyDisableKotlinStrictApiMode = true
}

