/*./material/material/build.gradle
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    androidTarget()
    desktop()
    mac()
    ios()
    js()
    wasmJs()

    // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
    linuxX64Stubs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                // TODO: AOSP does NOT have material-icons-core in dependencies. Should we keep it?

                implementation(libs.kotlinStdlib)
                api(project(":compose:animation:animation-core"))
                api(project(":compose:foundation:foundation"))
                api(project(":compose:material:material-ripple"))
                api(project(":compose:runtime:runtime"))
                api(project(":compose:ui:ui"))
                api(project(":compose:ui:ui-text"))

                implementation(project(":annotation:annotation"))
                implementation(project(":compose:animation:animation"))
                implementation(project(":compose:foundation:foundation-layout"))
                implementation(project(":compose:ui:ui-util"))
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlinTest)
                implementation(project(":compose:ui:ui-test"))
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        jvmTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.8.1")
                api("androidx.annotation:annotation-experimental:1.4.1")

                // TODO: remove next 3 dependencies when b/202810604 is fixed
                implementation("androidx.savedstate:savedstate:1.2.1")
                implementation("androidx.lifecycle:lifecycle-runtime:2.6.1")
                implementation("androidx.lifecycle:lifecycle-viewmodel:2.6.1")
            }
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":compose:foundation:foundation"))
                implementation(project(":compose:material:material:material-samples"))
                implementation(project(":compose:test-utils"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":test:screenshot:screenshot"))

                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.dexmakerMockito)
                implementation(libs.mockitoCore)
                implementation(libs.mockitoKotlin)
                implementation(libs.testUiautomator)
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
            }
        }

        // TODO: Align naming: nonAndroidMain
        skikoMain {
            dependsOn(commonMain)
            dependencies {
                implementation(libs.atomicFu)
            }
        }

        skikoTest {
            dependsOn(commonTest)
        }

        desktopMain {
            dependsOn(jvmMain)
            dependsOn(skikoMain)
        }

        desktopTest {
            dependsOn(jvmTest)
            dependsOn(skikoTest)
            dependencies {
                implementation(libs.skikoCurrentOs)
            }
        }

        // TODO: Align naming: nonJvmMain
        jsNativeMain {
            dependsOn(skikoMain)
        }

        jsNativeTest {
            dependsOn(skikoTest)
        }

        nativeMain {
            dependsOn(jsNativeMain)
        }

        nativeTest {
            dependsOn(jsNativeTest)
        }

        // TODO: Align naming: webMain
        jsWasmMain {
            dependsOn(jsNativeMain)
        }

        jsWasmTest {
            dependsOn(jsNativeTest)
        }

        jsMain {
            dependsOn(jsWasmMain)
        }

        jsTest {
            dependsOn(jsWasmTest)
        }

        wasmJsMain {
            dependsOn(jsWasmMain)
        }

        wasmJsTest {
            dependsOn(jsWasmTest)
        }

        targets.configureEach { target ->

            // TODO: Ignore stubs. Remove together with stubs target
            if (target.name.endsWith("Stubs")) return

            if (target.platformType == KotlinPlatformType.native) {
                target.compilations["main"].defaultSourceSet.dependsOn(nativeMain)
                target.compilations["test"].defaultSourceSet.dependsOn(nativeTest)
            }
        }

        // TODO: Align it with AOSP or make explicit
        configureEach {
            languageSettings.optIn("androidx.compose.material.ExperimentalMaterialApi")
        }

        // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
        commonStubsMain {
            dependsOn(commonMain)
        }

        linuxx64StubsMain {
            dependsOn(commonStubsMain)
        }
    }
}

dependencies {
    lintChecks(project(":compose:material:material-lint"))
    lintPublish(project(":compose:material:material-lint"))
}

androidx {
    name = "Compose Material Components"
    type = SoftwareType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    mavenVersion = LibraryVersions.COMPOSE
    inceptionYear = "2018"
    description = "Compose Material Design Components library"
    legacyDisableKotlinStrictApiMode = true
    metalavaK2UastEnabled = false
    samples(project(":compose:material:material:material-samples"))
    addGoldenImageAssets()
}

android {
    compileSdk = 35
    namespace = "androidx.compose.material"
}
// TODO: Restore updateTranslations task
