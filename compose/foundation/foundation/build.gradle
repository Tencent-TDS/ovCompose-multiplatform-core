/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jetbrains.kotlin.konan.target.Family

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    androidTarget()
    desktop()
    mac()
    ios()
    js()
    wasmJs()

    // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
    linuxX64Stubs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlib)
                api("androidx.collection:collection:1.5.0")
                api(project(":compose:animation:animation"))
                api(project(":compose:runtime:runtime"))
                api(project(":compose:ui:ui"))
                implementation(project(":compose:ui:ui-text"))
                implementation(project(":compose:ui:ui-util"))
                implementation(project(":compose:foundation:foundation-layout"))
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlinTest)
                implementation(libs.kotlinCoroutinesTest)
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        jvmTest {
            dependsOn(commonTest)
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.8.1")
                api("androidx.annotation:annotation-experimental:1.4.1")
                implementation("androidx.emoji2:emoji2:1.3.0")
                implementation("androidx.core:core:1.13.1")
            }
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":compose:test-utils"))
                implementation(project(":internal-testutils-fonts"))
                implementation(project(":test:screenshot:screenshot"))
                implementation(project(":internal-testutils-runtime"))
                implementation("androidx.activity:activity-compose:1.3.1")
                implementation("androidx.lifecycle:lifecycle-runtime:2.6.1")
                implementation("androidx.savedstate:savedstate:1.2.1")
                implementation("androidx.emoji2:emoji2-bundled:1.5.0")

                implementation(libs.testUiautomator)
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.testMonitor)
                implementation(libs.espressoCore)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.dexmakerMockito)
                implementation(libs.mockitoCore)
                implementation(libs.mockitoKotlin)

                implementation(libs.leakcanary)
                implementation(libs.leakcanaryInstrumentation)
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.kotlinReflect)
                implementation(libs.mockitoCore4)
                implementation(libs.mockitoKotlin4)
                implementation(libs.dexmakerMockitoInlineExtended)
                implementation(libs.byteBuddy)
                implementation(project(":constraintlayout:constraintlayout-compose"))
            }
        }

        // TODO: Align naming: nonAndroidMain
        skikoMain {
            dependsOn(commonMain)
            dependencies {
                implementation(libs.skiko)
                implementation(libs.atomicFu)
            }
        }

        skikoTest {
            dependsOn(commonTest)
        }

        desktopMain {
            dependsOn(jvmMain)
            dependsOn(skikoMain)
        }

        desktopTest {
            dependsOn(jvmTest)
            dependsOn(skikoTest)
            dependencies {
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(libs.truth)
                implementation(libs.junit)
                implementation(libs.skikoCurrentOs)
                implementation(libs.kotlinCoroutinesSwing)
                implementation(libs.mockitoCore4)
                implementation(libs.mockitoKotlin4)
                implementation(libs.mockitoCore)
                implementation(libs.mockitoKotlin)
            }
        }

        // TODO: Align naming: nonJvmMain
        jsNativeMain {
            dependsOn(skikoMain)
        }

        jsNativeTest {
            dependsOn(skikoTest)
        }

        nativeMain {
            dependsOn(jsNativeMain)
        }

        nativeTest {
            dependsOn(jsNativeTest)
        }

        darwinMain {
            dependsOn(nativeMain)
        }

        darwinTest {
            dependsOn(nativeTest)
        }

        macosMain {
            dependsOn(darwinMain)
        }

        macosTest {
            dependsOn(darwinTest)
        }

        uikitMain {
            dependsOn(darwinMain)
            dependencies {
                // TODO: We shouldn't use it directly here
                implementation(project(":compose:ui:ui-uikit"))
            }
        }

        uikitTest {
            dependsOn(darwinTest)
        }

        // TODO: Align naming: webMain
        jsWasmMain {
            dependsOn(jsNativeMain)
        }

        jsWasmTest {
            dependsOn(jsNativeTest)
        }

        jsMain {
            kotlin.srcDir("src/webCommonW3C/kotlin")
            dependsOn(jsWasmMain)
        }

        jsTest {
            dependsOn(jsWasmTest)
        }

        wasmJsMain {
            kotlin.srcDir("src/webCommonW3C/kotlin")
            dependsOn(jsWasmMain)
            dependencies {
                implementation(libs.kotlinXw3c)
            }
        }

        wasmJsTest {
            dependsOn(jsWasmTest)
        }

        targets.configureEach { target ->

            // TODO: Ignore stubs. Remove together with stubs target
            if (target.name.endsWith("Stubs")) return

            if (target.platformType == KotlinPlatformType.native) {
                if (target.konanTarget.family == Family.IOS) {
                    target.compilations["main"].defaultSourceSet.dependsOn(uikitMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(uikitTest)
                } else if (target.konanTarget.family == Family.OSX) {
                    target.compilations["main"].defaultSourceSet.dependsOn(macosMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(macosTest)
                } else if (target.konanTarget.family.appleFamily) {
                    target.compilations["main"].defaultSourceSet.dependsOn(darwinMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(darwinTest)
                } else {
                    target.compilations["main"].defaultSourceSet.dependsOn(nativeMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(nativeTest)
                }
            }
        }

        configureEach {
            languageSettings.optIn("androidx.compose.foundation.ExperimentalFoundationApi")
        }

        // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
        commonStubsMain {
            dependsOn(commonMain)
        }

        linuxx64StubsMain {
            dependsOn(commonStubsMain)
        }
    }
}

dependencies {
    lintChecks(project(":compose:foundation:foundation-lint"))
    lintPublish(project(":compose:foundation:foundation-lint"))
}

android {
    compileSdk = 35
    namespace = "androidx.compose.foundation"
    buildTypes.configureEach {
        consumerProguardFiles("proguard-rules.pro")
    }
    // TODO(b/345531033)
    experimentalProperties["android.lint.useK2Uast"] = false
}

androidx {
    name = "Compose Foundation"
    type = SoftwareType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    inceptionYear = "2018"
    description = "Higher level abstractions of the Compose UI primitives. This library is design system agnostic, providing the high-level building blocks for both application and design-system developers"
    legacyDisableKotlinStrictApiMode = true
    metalavaK2UastEnabled = false
    samples(project(":compose:foundation:foundation:foundation-samples"))
    addGoldenImageAssets()
}
