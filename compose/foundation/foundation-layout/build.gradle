/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.konan.target.Family

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    androidTarget()
    desktop()
    mac()
    ios()
    js()
    wasmJs()

    // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
    linuxX64Stubs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlib)
                api(project(":compose:ui:ui"))
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:ui:ui-util"))
                implementation(project(":collection:collection"))
                implementation(project(":compose:ui:ui-unit"))
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlinTest)
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        jvmTest {
            dependsOn(commonTest)
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.8.1")
                api("androidx.annotation:annotation-experimental:1.4.1")
                implementation("androidx.core:core:1.7.0")
                implementation("androidx.compose.animation:animation-core:1.2.1")
            }
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":compose:foundation:foundation"))
                implementation("androidx.compose.ui:ui-test-junit4:1.2.1")
                implementation(project(":compose:test-utils"))
                implementation("androidx.activity:activity-compose:1.3.1")
                implementation("androidx.activity:activity:1.9.1")

                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }
        }

        // TODO: Align naming: nonAndroidMain
        skikoMain {
            dependsOn(commonMain)
        }

        skikoTest {
            dependsOn(commonTest)
        }

        // TODO: Get rid of this. It should rely on compilation target
        notMobileMain {
            dependsOn(skikoMain)
        }

        notMobileTest {
            dependsOn(skikoTest)
        }

        desktopMain {
            dependsOn(jvmMain)
            dependsOn(skikoMain)
            dependsOn(notMobileMain)
        }

        desktopTest {
            dependsOn(jvmTest)
            dependsOn(skikoTest)
            dependsOn(notMobileTest)
        }

        // TODO: Align naming: nonJvmMain
        jsNativeMain {
            dependsOn(skikoMain)
        }

        jsNativeTest {
            dependsOn(skikoTest)
        }

        nativeMain {
            dependsOn(jsNativeMain)
        }

        nativeTest {
            dependsOn(jsNativeTest)
        }

        uikitMain {
            dependsOn(nativeMain)
        }

        uikitTest {
            dependsOn(nativeTest)
        }

        macosMain {
            dependsOn(nativeMain)
            dependsOn(notMobileMain)
        }

        macosTest {
            dependsOn(nativeTest)
        }

        webMain {
            dependsOn(jsNativeMain)
            dependsOn(notMobileMain)
        }

        webTest {
            dependsOn(jsNativeTest)
        }

        targets.configureEach { target ->

            // TODO: Ignore stubs. Remove together with stubs target
            if (target.name.endsWith("Stubs")) return

            if (target.platformType == KotlinPlatformType.native) {
                if (target.konanTarget.family == Family.IOS) {
                    target.compilations["main"].defaultSourceSet.dependsOn(uikitMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(uikitTest)
                } else if (target.konanTarget.family == Family.OSX) {
                    target.compilations["main"].defaultSourceSet.dependsOn(macosMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(macosTest)
                } else {
                    target.compilations["main"].defaultSourceSet.dependsOn(nativeMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(nativeTest)
                }
            } else if (target.platformType in [KotlinPlatformType.js, KotlinPlatformType.wasm]) {
                target.compilations["main"].defaultSourceSet.dependsOn(webMain)
                target.compilations["test"].defaultSourceSet.dependsOn(webTest)
            }
        }

        // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
        commonStubsMain {
            dependsOn(commonMain)
        }

        linuxx64StubsMain {
            dependsOn(commonStubsMain)
        }
    }
}

androidx {
    name = "Compose Layouts"
    type = SoftwareType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    inceptionYear = "2019"
    description = "Compose layout implementations"
    legacyDisableKotlinStrictApiMode = true
    metalavaK2UastEnabled = false
    samples(project(":compose:foundation:foundation-layout:foundation-layout-samples"))
}

android {
    compileSdk = 35
    namespace = "androidx.compose.foundation.layout"
    buildTypes.configureEach {
        consumerProguardFiles("proguard-rules.pro")
    }
}
