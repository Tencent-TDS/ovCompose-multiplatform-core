/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */


import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    androidTarget()
    desktop()
    mac()
    ios()
    js()
    wasmJs()

    // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
    linuxX64Stubs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:ui:ui"))
                implementation(project(":compose:ui:ui-unit"))
                implementation(project(":compose:ui:ui-graphics"))
                implementation(project(":compose:ui:ui-util"))
                implementation(project(":collection:collection"))
                implementation(libs.kotlinStdlib)
                api(libs.kotlinCoroutinesCore)
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlinTest)
                implementation(libs.kotlinCoroutinesTest)
                implementation(project(":kruth:kruth"))
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        jvmTest {
            dependsOn(commonTest)
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.8.1")
            }
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.testCore)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(project(":compose:animation:animation"))
                implementation(project(":compose:foundation:foundation"))
                implementation("androidx.compose.ui:ui-test-junit4:1.2.1")
                implementation(project(":compose:test-utils"))
                implementation("androidx.compose.material3:material3:1.2.1")
                implementation(libs.leakcanary)
                implementation(libs.leakcanaryInstrumentation)
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.kotlinCoroutinesCore)
            }
        }

        // TODO: Align naming: nonAndroidMain
        jbMain {
            dependsOn(commonMain)
        }

        jbTest {
            dependsOn(commonTest)
        }

        desktopMain {
            dependsOn(jvmMain)
            dependsOn(jbMain)
        }

        desktopTest {
            dependsOn(jvmTest)
            dependsOn(jbTest)
        }

        // TODO: Align naming: nonJvmMain
        jsNativeMain {
            dependsOn(jbMain)
        }

        jsNativeTest {
            dependsOn(jbTest)
        }

        nativeMain {
            dependsOn(jsNativeMain)
        }

        nativeTest {
            dependsOn(jsNativeTest)
        }

        webMain {
            dependsOn(jsNativeMain)
        }

        webTest {
            dependsOn(jsNativeTest)
        }

        targets.configureEach { target ->

            // TODO: Ignore stubs. Remove together with stubs target
            if (target.name.endsWith("Stubs")) return

            if (target.platformType == KotlinPlatformType.native) {
                target.compilations["main"].defaultSourceSet.dependsOn(nativeMain)
                target.compilations["test"].defaultSourceSet.dependsOn(nativeTest)
            } else if (target.platformType in [KotlinPlatformType.js, KotlinPlatformType.wasm]) {
                target.compilations["main"].defaultSourceSet.dependsOn(webMain)
                target.compilations["test"].defaultSourceSet.dependsOn(webTest)
            }
        }

        // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
        commonStubsMain {
            dependsOn(commonMain)
        }

        linuxx64StubsMain {
            dependsOn(commonStubsMain)
        }
    }
}

dependencies {
    lintPublish(project(":compose:animation:animation-core-lint"))
}

androidx {
    name = "Compose Animation Core"
    type = SoftwareType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    inceptionYear = "2019"
    description = "Animation engine and animation primitives that are the building blocks of the Compose animation library"
    metalavaK2UastEnabled = false
    samples(project(":compose:animation:animation-core:animation-core-samples"))
}

android {
    compileSdk = 35
    namespace = "androidx.compose.animation.core"
    buildTypes.configureEach {
        consumerProguardFiles("proguard-rules.pro")
    }
}
