/*
 * Copyright 2019 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */


import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.konan.target.Family

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    androidTarget()
    desktop()
    mac()
    ios()
    js()
    wasmJs()

    // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
    linuxX64Stubs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlib)
                implementation(libs.kotlinCoroutinesCore)

                api(project(":compose:ui:ui-graphics"))
                api(project(":compose:ui:ui-unit"))

                // when updating the runtime version please also update the runtime-saveable version
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:runtime:runtime-saveable"))

                implementation(project(":compose:ui:ui-util"))

                api("androidx.annotation:annotation:1.9.1")
                implementation("androidx.collection:collection:1.5.0")
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlinTest)
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        jvmTest {
            dependsOn(commonTest)
        }

        androidMain {
            dependsOn(commonMain)
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation-experimental:1.4.1")
                implementation("androidx.core:core:1.7.0")
                implementation("androidx.emoji2:emoji2:1.4.0")
            }
        }

        androidInstrumentedTest {
            dependsOn(jvmTest)
            dependencies {
                implementation("androidx.emoji2:emoji2-bundled:1.4.0")
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":internal-testutils-fonts"))
                implementation(project(":compose:foundation:foundation"))
                implementation(project(":compose:test-utils"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.espressoCore)
                implementation(libs.junit)
                implementation(libs.dexmakerMockito)
                implementation(libs.mockitoCore)
                implementation(libs.truth)
                implementation(libs.mockitoKotlin)
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(project(":internal-testutils-fonts"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.kotlinReflect)
                implementation(libs.kotlinTest)
                implementation(libs.mockitoCore4)
                implementation(libs.mockitoKotlin4)
                implementation(libs.byteBuddy)
            }
        }

        // TODO: Align naming: nonAndroidMain
        skikoMain {
            dependsOn(commonMain)
            dependencies {
                api(libs.skiko)
                implementation(libs.atomicFu)
            }
        }

        skikoTest {
            dependsOn(commonTest)
        }

        desktopMain {
            dependsOn(jvmMain)
            dependsOn(skikoMain)
        }

        desktopTest {
            dependsOn(jvmTest)
            dependsOn(skikoTest)
            kotlin.srcDirs("src/test/java", "src/desktopTest/kotlin")
            resources.srcDirs("src/test/resources", "src/desktopTest/resources")
            dependencies {
                implementation(libs.truth)
                implementation(libs.junit)
                implementation(libs.skikoCurrentOs)
                implementation(project(":compose:foundation:foundation"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":internal-testutils-fonts"))
                implementation(libs.mockitoCore)
                implementation(libs.mockitoKotlin)
                implementation(libs.mockitoCore4)
                implementation(libs.mockitoKotlin4)
                implementation(libs.kotlinReflect)
            }
        }

        nonJvmMain {
            dependsOn(skikoMain)
        }

        nonJvmTest {
            dependsOn(skikoTest)
        }

        nativeMain {
            dependsOn(nonJvmMain)
        }

        nativeTest {
            dependsOn(nonJvmTest)
        }

        darwinMain {
            dependsOn(nativeMain)
        }

        darwinTest {
            dependsOn(nativeTest)
        }

        macosMain {
            dependsOn(darwinMain)
        }

        macosTest {
            dependsOn(darwinTest)
        }

        uikitMain {
            dependsOn(darwinMain)
        }

        uikitTest {
            dependsOn(darwinTest)
        }

        webMain {
            dependsOn(nonJvmMain)
        }

        webTest {
            dependsOn(nonJvmTest)
        }

        jsMain {
            kotlin.srcDir("src/webCommonW3C/kotlin")
        }

        wasmJsMain {
            kotlin.srcDir("src/webCommonW3C/kotlin")
            dependencies {
                implementation(libs.skikoWasmJs)
                implementation(libs.skikoJsWasmRuntime)
            }
        }

        targets.configureEach { target ->

            // TODO: Ignore stubs. Remove together with stubs target
            if (target.name.endsWith("Stubs")) return

            if (target.platformType == KotlinPlatformType.native) {
                if (target.konanTarget.family == Family.IOS) {
                    target.compilations["main"].defaultSourceSet.dependsOn(uikitMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(uikitTest)
                } else if (target.konanTarget.family == Family.OSX) {
                    target.compilations["main"].defaultSourceSet.dependsOn(macosMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(macosTest)
                } else if (target.konanTarget.family.appleFamily) {
                    target.compilations["main"].defaultSourceSet.dependsOn(darwinMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(darwinTest)
                } else {
                    target.compilations["main"].defaultSourceSet.dependsOn(nativeMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(nativeTest)
                }
            } else if (target.platformType in [KotlinPlatformType.js, KotlinPlatformType.wasm]) {
                target.compilations["main"].defaultSourceSet.dependsOn(webMain)
                target.compilations["test"].defaultSourceSet.dependsOn(webTest)
            }
        }

        // TODO: Align it with AOSP or make explicit
        configureEach {
            languageSettings.optIn("androidx.compose.ui.text.ExperimentalTextApi")
        }

        // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
        commonStubsMain {
            dependsOn(commonMain)
        }
        linuxx64StubsMain {
            dependsOn(commonStubsMain)
        }
    }
}

dependencies {
    lintPublish(project(":compose:ui:ui-text-lint"))
    lintChecks(project(":compose:ui:ui-text-lint"))
}

androidx {
    name = "Compose UI Text"
    type = SoftwareType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    inceptionYear = "2019"
    description = "Compose Text primitives and utilities"
    legacyDisableKotlinStrictApiMode = true
    metalavaK2UastEnabled = false
    samples(project(":compose:ui:ui-text:ui-text-samples"))
}

android {
    compileSdk = 35
    namespace = "androidx.compose.ui.text"
    // TODO(b/328001575)
    experimentalProperties["android.lint.useK2Uast"] = false
    buildTypes.configureEach {
        consumerProguardFiles("proguard-rules.pro")
    }
}
