/*
 * Copyright 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.AndroidXComposePlugin
import androidx.build.JetbrainsAndroidXPlugin
import androidx.build.LibraryType
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
    id("JetbrainsAndroidXPlugin")
}

AndroidXComposePlugin.applyAndConfigureKotlinPlugin(project)
JetbrainsAndroidXPlugin.applyAndConfigure(project)

if(!AndroidXComposePlugin.isMultiplatformEnabled(project)) {
    dependencies {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block below
         */

        api("androidx.annotation:annotation:1.2.0")
        api(project(":compose:ui:ui-unit"))

        implementation("androidx.compose.runtime:runtime:1.2.1")
        implementation(project(":compose:ui:ui-util"))
        implementation(libs.kotlinStdlib)

        testImplementation(libs.testRules)
        testImplementation(libs.testRunner)
        testImplementation(libs.kotlinTestJunit)
        testImplementation(libs.junit)
        testImplementation(libs.truth)

        androidTestImplementation(project(":compose:ui:ui-graphics:ui-graphics-samples"))
        androidTestImplementation(project(":compose:ui:ui-test-junit4"))
        androidTestImplementation(project(":compose:test-utils"))
        androidTestImplementation(libs.testRules)
        androidTestImplementation(libs.testRunner)
        androidTestImplementation(libs.espressoCore)
        androidTestImplementation(libs.junit)
        androidTestImplementation(libs.truth)

        lintPublish(project(":compose:ui:ui-graphics-lint"))

        samples(projectOrArtifact(":compose:ui:ui-graphics:ui-graphics-samples"))
    }
}

if(AndroidXComposePlugin.isMultiplatformEnabled(project)) {
    androidXComposeMultiplatform {
        android()
        desktop()
        js()
        wasm()
        darwin()

        configureDarwinFlags()
    }

    kotlin {
        /*
         * When updating dependencies, make sure to make the an an analogous update in the
         * corresponding block above
         */

        sourceSets {
            commonMain.dependencies {
                implementation(libs.kotlinStdlib)

                api(project(":compose:ui:ui-unit"))
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:ui:ui-util"))
                implementation(project(":compose:ui:ui-geometry"))
                implementation(project(":annotation:annotation"))
                implementation(project(":collection:collection"))
            }

            androidMain.dependencies {
                api("androidx.annotation:annotation:1.2.0")
            }

            skikoMain {
                dependsOn(commonMain)
                dependencies {
                    api(libs.skikoCommon)
                }
            }

            skikoExcludingWebMain {
                dependsOn(skikoMain)
            }

            jsNativeMain.dependsOn(skikoMain)

            jsWasmMain.dependsOn(jsNativeMain)

            jsMain {
                dependsOn(jsWasmMain)
            }

            wasmJsMain {
                dependsOn(jsWasmMain)
                dependencies {
                    implementation(libs.kotlinStdlib)
                    implementation(libs.create("skikoWasm"))
                    implementation(libs.create("skikoWasmWasm"))
                }
            }

            desktopMain {
                dependsOn skikoExcludingWebMain
                dependencies {
                    implementation(libs.kotlinStdlib)
                    implementation(libs.kotlinStdlibJdk8)
                }
            }

            commonTest {
                dependencies {
                    implementation(libs.kotlinTest)
                }
            }

            nativeMain.dependsOn(jsNativeMain)
            nativeMain.dependsOn(skikoExcludingWebMain)

            // TODO(b/214407011): These dependencies leak into instrumented tests as well. If you
            //  need to add Robolectric (which must be kept out of androidAndroidTest), use a top
            //  level dependencies block instead:
            //  `dependencies { testImplementation(libs.robolectric) }`
            androidTest.dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
            }

            androidAndroidTest.dependencies {
                implementation(project(":compose:ui:ui-graphics:ui-graphics-samples"))
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(project(":compose:test-utils"))
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.espressoCore)
                implementation(libs.junit)
            }

            skikoTest {
                dependsOn(commonTest)
                dependencies {
                    implementation(project(":compose:ui:ui-test-junit4"))
                }
            }

            desktopTest {
                dependsOn(skikoTest)
                resources.srcDirs += "src/desktopTest/res"
                dependencies {
                    implementation(libs.junit)
                    implementation(libs.truth)
                    implementation(libs.skikoCurrentOs)
                }
            }
            nativeTest {
                dependsOn(skikoTest)
            }

            jsTest {
                dependsOn(skikoTest)
            }

            wasmJsTest {
                dependsOn(skikoTest)
            }

            configureEach {
                languageSettings.optIn("androidx.compose.ui.ExperimentalComposeUiApi")
                languageSettings.optIn("androidx.compose.ui.InternalComposeUiApi")
            }
        }
    }
    dependencies {
        samples(projectOrArtifact(":compose:ui:ui-graphics:ui-graphics-samples"))
    }
}

androidx {
    name = "Compose Graphics"
    type = LibraryType.PUBLISHED_LIBRARY
    inceptionYear = "2020"
    description = "Compose graphics"
    legacyDisableKotlinStrictApiMode = true
}

androidxCompose {
    composeCompilerPluginEnabled = false
}

android {
    namespace "androidx.compose.ui.graphics"
}

if(AndroidXComposePlugin.isMultiplatformEnabled(project)) {
    tasks.findByName("desktopTest").configure {
        systemProperties["GOLDEN_PATH"] = project.rootDir.absolutePath + "/golden"
    }
}
