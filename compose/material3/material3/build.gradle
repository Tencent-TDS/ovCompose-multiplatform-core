/*
 * Copyright (C) 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * This file was created using the `create_project.py` script located in the
 * `<AndroidX root>/development/project-creator` directory.
 *
 * Please use that script when creating a new project, rather than copying an existing project and
 * modifying its settings.
 */

import androidx.build.SoftwareType
import androidx.build.PlatformIdentifier
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.konan.target.Family

plugins {
    id("AndroidXPlugin")
    id("com.android.library")
    id("AndroidXComposePlugin")
}

androidXMultiplatform {
    androidTarget()
    desktop()
    mac()
    ios()
    js()
    wasmJs()

    // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
    linuxX64Stubs()

    defaultPlatform(PlatformIdentifier.ANDROID)

    sourceSets {
        commonMain {
            dependencies {
                implementation(libs.kotlinStdlib)
                // Keep pinned unless there is a need for tip of tree behavior
                implementation(project(":collection:collection"))
                implementation(project(":compose:animation:animation-core"))
                implementation(project(":compose:ui:ui-util"))
                api(project(":compose:foundation:foundation-layout"))
                api(project(":compose:foundation:foundation"))
                api(project(":compose:material:material-ripple"))
                api(project(":compose:runtime:runtime"))
                api(project(":compose:ui:ui"))
                api(project(":compose:ui:ui-text"))
                api(project(":graphics:graphics-shapes"))
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlinTest)
                implementation(project(":compose:ui:ui-test"))
            }
        }

        jvmMain {
            dependsOn(commonMain)
        }

        jvmTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":compose:ui:ui-test-junit4"))
                implementation(libs.truth)
                implementation(libs.junit)
            }
        }

        androidMain {
            dependsOn(jvmMain)
            dependencies {
                api("androidx.annotation:annotation:1.8.1")
                api("androidx.annotation:annotation-experimental:1.4.1")
                implementation("androidx.activity:activity-compose:1.8.2")
                implementation("androidx.lifecycle:lifecycle-common-java8:2.6.1")
            }
        }

        androidUnitTest {
            dependsOn(jvmTest)
            dependencies {
                implementation(libs.testRules)
                implementation(libs.testRunner)
            }
        }

        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation(project(":compose:material3:material3:material3-samples"))
                implementation(project(":compose:test-utils"))
                implementation(project(":compose:runtime:runtime"))
                implementation(project(":compose:foundation:foundation-layout"))
                implementation(project(":compose:foundation:foundation"))
                implementation("androidx.compose.material:material-icons-core:1.7.5")
                implementation(project(":test:screenshot:screenshot"))
                implementation("androidx.core:core:1.15.0")
                implementation("androidx.compose.ui:ui-tooling:1.4.1")
                implementation(libs.espressoCore)
                implementation(libs.testRules)
                implementation(libs.testRunner)
                implementation(libs.junit)
                implementation(libs.truth)
                implementation(libs.dexmakerMockitoInlineExtended)
                implementation(libs.mockitoKotlin)
                implementation(libs.testUiautomator)
            }
        }

        // TODO: Align naming: nonAndroidMain
        skikoMain {
            dependsOn(commonMain)
            dependencies {
                implementation(libs.datetime)
                implementation(project(":compose:ui:ui-backhandler"))
            }
        }

        skikoTest {
            dependsOn(commonTest)
        }

        desktopMain {
            dependsOn(jvmMain)
            dependsOn(skikoMain)
        }

        desktopTest {
            dependsOn(jvmTest)
            dependsOn(skikoTest)
            dependencies {
                implementation(libs.skikoCurrentOs)
            }
        }

        nonJvmMain {
            dependsOn(skikoMain)
            dependencies {
                implementation(libs.atomicFu)
            }
        }

        nonJvmTest {
            dependsOn(skikoTest)
        }

        nativeMain {
            dependsOn(nonJvmMain)
        }

        nativeTest {
            dependsOn(nonJvmTest)
        }

        darwinMain {
            dependsOn(nativeMain)
        }

        darwinTest {
            dependsOn(nativeTest)
        }

        uikitMain {
            dependsOn(darwinMain)
        }

        uikitTest {
            dependsOn(darwinTest)
        }

        // TODO: Align naming: webMain
        jsWasmMain {
            dependsOn(nonJvmMain)
        }

        jsWasmTest {
            dependsOn(nonJvmTest)
        }

        jsMain {
            kotlin.srcDir("src/webCommonW3C/kotlin")
            dependsOn(jsWasmMain)
        }

        jsTest {
            dependsOn(jsWasmTest)
        }

        wasmJsMain {
            kotlin.srcDir("src/webCommonW3C/kotlin")
            dependsOn(jsWasmMain)
            dependencies {
                implementation(libs.kotlinXw3c)
            }
        }

        wasmJsTest {
            dependsOn(jsWasmTest)
        }

        targets.configureEach { target ->

            // TODO: Ignore stubs. Remove together with stubs target
            if (target.name.endsWith("Stubs")) return

            if (target.platformType == KotlinPlatformType.native) {
                if (target.konanTarget.family == Family.IOS) {
                    target.compilations["main"].defaultSourceSet.dependsOn(uikitMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(uikitTest)
                } else if (target.konanTarget.family.appleFamily) {
                    target.compilations["main"].defaultSourceSet.dependsOn(darwinMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(darwinTest)
                } else {
                    target.compilations["main"].defaultSourceSet.dependsOn(nativeMain)
                    target.compilations["test"].defaultSourceSet.dependsOn(nativeTest)
                }
            }
        }

        // TODO: Needs to be removed. Keep it for correct configuration of dependent modules.
        commonStubsMain {
            dependsOn(commonMain)
        }

        linuxx64StubsMain {
            dependsOn(commonStubsMain)
        }

        // TODO: Align it with AOSP or make explicit
        configureEach {
            languageSettings.optIn("androidx.compose.material3.ExperimentalMaterial3Api")
        }
    }
}

dependencies {
    lintChecks(project(":compose:material3:material3-lint"))
    lintPublish(project(":compose:material3:material3-lint"))
}

androidx {
    name = "Compose Material3 Components"
    type = SoftwareType.PUBLISHED_LIBRARY_ONLY_USED_BY_KOTLIN_CONSUMERS
    inceptionYear = "2021"
    description = "Compose Material You Design Components library"
    legacyDisableKotlinStrictApiMode = true
    metalavaK2UastEnabled = false
    samples(project(":compose:material3:material3:material3-samples"))
    addGoldenImageAssets()
}

android {
    compileSdk = 35
    namespace = "androidx.compose.material3"
    // TODO(b/345531033)
    experimentalProperties["android.lint.useK2Uast"] = false
}
